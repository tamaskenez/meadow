cmake_minimum_required(VERSION 3.29)
project(meadow)

if(PROJECT_IS_TOP_LEVEL)
    if(NOT CMAKE_PREFIX_PATH)
        set(CMAKE_PREFIX_PATH ${CMAKE_CURRENT_SOURCE_DIR}/id)
    endif()

    set(CMAKE_FIND_PACKAGE_PREFER_CONFIG ON)
    set(CMAKE_CXX_STANDARD 23)
    set(CMAKE_CXX_STANDARD_REQUIRED ON)

    option(BUILD_TESTING "Build the testing tree." ON)
    if (BUILD_TESTING)
        enable_testing()
        find_package(GTest REQUIRED CONFIG)
    endif()

    set(CMAKE_COMPILE_WARNING_AS_ERROR ON)

    include(${CMAKE_SOURCE_DIR}/cmake/warnings_clang.cmake)
    include(${CMAKE_SOURCE_DIR}/cmake/warnings_gcc.cmake)
    include(${CMAKE_SOURCE_DIR}/cmake/warnings_msvc.cmake)

    set(dependency_default_requirement ON)
else()
    set(dependency_default_requirement AUTO)
endif()

set(dependencies
    ABSL
    EIGEN
    SDL
    SDL_MIXER
    BOOST
)

set(ABSL_PACKAGE_NAME absl)
set(ABSL_TARGET absl_base)
set(EIGEN_PACKAGE_NAME Eigen3)
set(SDL_PACKAGE_NAME SDL3)
set(SDL_TARGET SDL3::SDL3)
set(SDL_MIXER_PACKAGE_NAME SDL3_mixer)
set(SDL_MIXER_TARGET SDL3_mixer::SDL3_mixer)
set(BOOST_PACKAGE_NAME Boost)

foreach(dependency IN LISTS dependencies)
    set(MEADOW_WITH_${dependency} ${dependency_default_requirement} CACHE STRING "Build with ${dependency}")
    if(TARGET "${${dependency}_TARGET}")
        set(target_found 1)
        set(found_where "as target ${${dependency}_TARGET}")
    else()
        set(target_found 0)
    endif()
    if(MEADOW_WITH_${dependency} STREQUAL "AUTO")
        if(NOT target_found)
            find_package(${${dependency}_PACKAGE_NAME} CONFIG)
        endif()
        if(target_found OR ${${dependency}_PACKAGE_NAME}_FOUND)
            if(NOT target_found)
                set(found_where "in ${${${dependency}_PACKAGE_NAME}_DIR}")
            endif()
            message(STATUS "Dependency ${dependency} is set to AUTO and it is FOUND ${found_where}")
            set(MEADOW_HAS_${dependency} 1)
        else()
            message(STATUS "Dependency ${dependency} is set to AUTO and it is NOT FOUND")
            set(MEADOW_HAS_${dependency} 0)
        endif()
    elseif(MEADOW_WITH_${dependency})
        if(NOT target_found)
            find_package(${${dependency}_PACKAGE_NAME} REQUIRED CONFIG)
            set(found_where "in ${${${dependency}_PACKAGE_NAME}_DIR}")
        endif()
        message(STATUS "Dependency ${dependency} is ON and it is FOUND ${found_where}")
        set(MEADOW_HAS_${dependency} 1)
    else()
        message(STATUS "Dependency ${dependency} is OFF")
        set(MEADOW_HAS_${dependency} 0)
    endif()
endforeach()

if(MEADOW_HAS_SDL_MIXER AND NOT MEADOW_HAS_SDL)
    message(FATAL_ERROR "Can't build with SDL_MIXER but without SDL")
endif()

add_subdirectory(src)
if(BUILD_TESTING)
    find_package(magic_enum REQUIRED CONFIG)
    add_subdirectory(testing)
endif()
